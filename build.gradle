// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // shared build properties
        kotlinVersion       = "1.3.71"
        buildToolsVersion   = "29.0.3"
        minSdkVersion       = 21
        targetSdkVersion    = 29
        compileSdkVersion   = 29

        versionName         = "3.2"
        versionCode         = 53

        xikoloApiVersion    = 4
        realmSchemaVersion  = 14
    }
    repositories {
        jcenter()
        google()
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "com.google.gms:google-services:4.3.3"
        classpath "io.fabric.tools:gradle:1.31.2"
        classpath "io.realm:realm-gradle-plugin:6.1.0"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.28.0"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url "https://jitpack.io" }
        maven { url "https://clojars.org/repo/" }
        flatDir {
            dirs "libs"
        }
    }
}

ext {
    // travis ci
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true" == System.getProperty("pre-dex", "true")
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
